apply plugin: 'groovy'

dependencies {
	compile gradleApi()
	compile gradleTestKit()
	compile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.3.0"
	compile group: "com.squareup.okhttp3", name: "okhttp", version: "3.3.1"
	compile group: "org.codehaus.groovy", name: "groovy-all", version: "2.4.4"
	compile group: "org.spockframework", name: "spock-core", version: "1.0-groovy-2.4"

	testCompile project(":tests:utils")
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

startTestableTomcat {
	checkTimeout = 30 * 60 * 1000
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

testIntegration {
	outputs.upToDateWhen { false }

	beforeSuite {
		def moduleOutputPaths = []

		rootProject.subprojects { p ->
			def tempOutput

			p.plugins.withType(WarPlugin) {
				tempOutput = p.tasks["war"].outputs.files.singleFile.absolutePath
			}

			if (tempOutput == null) {
				p.plugins.withType(JavaPlugin) {
					tempOutput = p.tasks["jar"].outputs.files.singleFile.absolutePath
				}
			}

			if (tempOutput != null) {
				moduleOutputPaths << tempOutput
			}

		}

		moduleOutputPaths.removeAll { it.contains("test") }

		systemProperty 'moduleOutputPaths', moduleOutputPaths.unique().join(",")
	}

	rootProject.subprojects { p ->
		p.plugins.withType(JavaPlugin) {
			dependsOn p.tasks["jar"]
		}

		p.plugins.withType(WarPlugin) {
			dependsOn p.tasks["war"]
		}

	}

	systemProperty 'bladeURL', System.getProperty('bladeURL', "https://repository.liferay.com/nexus/service/local/artifact/maven/redirect?r=liferay-public-releases&g=com.liferay.blade&a=com.liferay.blade.cli&v=LATEST")
	systemProperty 'portalVersion', project.getProperty("liferay.workspace.bundle.url")

	finalizedBy {
		stopTestableTomcat
	}

	doLast {
		delete new File("target/drone-phantomjs")
	}
}