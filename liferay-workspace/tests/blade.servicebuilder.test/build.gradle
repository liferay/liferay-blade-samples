sourceSets {
	testFunctional {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/testFunctional/java')
		}
		resources.srcDir file('src/testFunctional/resources')
	}
}

configurations {
	testFunctionalCompile.extendsFrom testCompile
	testFunctionalRuntime.extendsFrom testRuntime
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "3.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "osgi.cmpn", version: "6.0.0"
	compileOnly project(":apps:service-builder:basic:basic-api")

	testCompile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.5.0"
	testCompile group: "biz.aQute.bnd", name: "biz.aQute.remote.api", version: "3.5.0"
	testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "org.jboss.arquillian.graphene", name: "graphene-webdriver", version: "2.3.2"
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"

	testCompile (project(':tests:utils')) {
		transitive = false
	}
}

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000
}

task testFunctional(type: Test) {
	testClassesDir = sourceSets.testFunctional.output.classesDir
	classpath = sourceSets.testFunctional.runtimeClasspath
	outputs.upToDateWhen { false }

	dependsOn jar, startTestableTomcat

	project(':apps:service-builder:basic:basic-api').afterEvaluate { project ->
		dependsOn project.jar
		systemProperty 'fooApiJarFile', project.jar.archivePath
	}

	project(':apps:service-builder:basic:basic-service').afterEvaluate { project ->
		dependsOn project.jar
		systemProperty 'fooServiceJarFile', project.jar.archivePath
	}

	project(':apps:service-builder:basic:basic-web').afterEvaluate { project ->
		dependsOn project.jar
		systemProperty 'fooWebJarFile', project.jar.archivePath
	}

	project(':wars:springmvc-portlet').afterEvaluate { project ->
		dependsOn project.war
		systemProperty 'springmvcPortletWarFile', project.war.archivePath
	}

	systemProperty 'bladeURL', System.getProperty('bladeURL', "https://repository.liferay.com/nexus/service/local/artifact/maven/redirect?r=liferay-public-releases&g=com.liferay.blade&a=com.liferay.blade.cli&v=LATEST")
	systemProperty 'jarFile', jar.archivePath

	doLast {
		delete new File("blade.jar")
		delete new File("target")
	}

	finalizedBy {
		stopTestableTomcat
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

check.dependsOn testFunctional