apply plugin: 'groovy'

dependencies {
	compile gradleApi()
	compile gradleTestKit()
	compile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.1.0"
	compile group: "com.squareup.okhttp3", name: "okhttp", version: "3.3.1"
	compile group: "org.codehaus.groovy", name: "groovy-all", version: "2.4.4"
	compile group: "org.spockframework", name: "spock-core", version: "1.0-groovy-2.4"

	testCompile group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.20.0"
	testCompile group: "commons-io", name: "commons-io", version: "2.5"
}

repositories {
	maven {
		url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
	}
}

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000
}

testIntegration {
	outputs.upToDateWhen { false }

	dependsOn ":initBundle", startTestableTomcat

	beforeSuite {
		def moduleOutputPaths = []

		project(":modules").subprojects { p ->
			p.plugins.withType(JavaPlugin) {
				moduleOutputPaths << p.tasks["jar"].outputs.files.singleFile.absolutePath
			}
		}

		systemProperty 'moduleOutputPaths', moduleOutputPaths.join(",")
	}

	project(":modules").subprojects { p ->
		p.plugins.withType(JavaPlugin) {
			dependsOn p.tasks["build"]
		}
	}

	systemProperty 'bladeURL', System.getProperty('bladeURL', "https://liferay-test-01.ci.cloudbees.com/job/liferay-blade-cli/150/artifact/build/generated/p2/plugins/com.liferay.blade.cli_2.2.0.201706201908.jar")

	finalizedBy {
		stopTestableTomcat
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}