sourceSets {
    testFunctional {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testFunctional/java')
        }
        resources.srcDir file('src/testFunctional/resources')
    }
}

configurations {
    testFunctionalCompile.extendsFrom testCompile
    testFunctionalRuntime.extendsFrom testRuntime
}

dependencies {
	compileOnly  project(':modules:blade.servicebuilder.api')
	compileOnly 'com.liferay.portal:com.liferay.portal.kernel:2.0.0'
	compileOnly 'javax.portlet:portlet-api:2.0'
	compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
	compileOnly 'org.osgi:osgi.cmpn:6.0.0'

	testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "org.jboss.arquillian.graphene", name: "graphene-webdriver", version: "2.1.0.Final"
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"
}

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000
}

task testFunctional(type: Test) {
    testClassesDir = sourceSets.testFunctional.output.classesDir
    classpath = sourceSets.testFunctional.runtimeClasspath
    outputs.upToDateWhen { false }

	dependsOn jar, ":initBundle", startTestableTomcat
	dependsOn ":modules:blade.servicebuilder.api:deploy"
	dependsOn ":modules:blade.servicebuilder.svc:deploy"
	dependsOn ":modules:blade.servicebuilder.web:deploy"

	systemProperty 'jarFile', jar.archivePath

	finalizedBy {
		stopTestableTomcat
	}

	doLast {
		delete new File("target/drone-phantomjs")
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

check.dependsOn testFunctional