sourceSets {
	testFunctional {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/testFunctional/java')
		}
		resources.srcDir file('src/testFunctional/resources')
	}
}

configurations {
	autoLogin
	testFunctionalCompile.extendsFrom testCompile
	testFunctionalRuntime.extendsFrom testRuntime
}

dependencies {
	autoLogin project(':tests:test-auto-login')

	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "3.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"

	testCompile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.5.0"
	testCompile group: "biz.aQute.bnd", name: "biz.aQute.remote.api", version: "3.5.0"
	testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: 'org.jboss.arquillian.graphene', name: 'graphene-webdriver', version: '2.3.2'
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"

	testCompile (project(':tests:utils')) {
		transitive = false
	}
}

task deployAutoLogin(type: Copy) {
	from configurations.autoLogin
	into new File(liferay.liferayHome, "osgi/modules")
}

deployAutoLogin.dependsOn ":tests:test-auto-login:jar"

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000

	finalizedBy {
		deployAutoLogin
	}

}

task testFunctional(type: Test) {
	testClassesDir = sourceSets.testFunctional.output.classesDir
	classpath = sourceSets.testFunctional.runtimeClasspath
	outputs.upToDateWhen { false }

	dependsOn startTestableTomcat

	dependsOn ":apps:action-command-portlet:jar"
	dependsOn ":apps:configuration-action:jar"
	dependsOn ":apps:control-panel-portlet:jar"
	dependsOn ":apps:ds-portlet:jar"
	dependsOn ":apps:filter-portlet:jar"
	dependsOn ":apps:freemarker-portlet:jar"
	dependsOn ":apps:greedy-policy-option-portlet:higher-ranked-service:jar"
	dependsOn ":apps:greedy-policy-option-portlet:service-reference:jar"
	dependsOn ":apps:jsp-portlet:jar"
	dependsOn ":apps:osgi-portlet:jar"
	dependsOn ":apps:render-command-portlet:jar"
	dependsOn ":apps:rest:jar"
	dependsOn ":apps:shared-language-keys:language:jar"
	dependsOn ":apps:shared-language-keys:language-web:jar"
	dependsOn ":extensions:control-menu-entry:jar"
	dependsOn ":extensions:servlet:jar"
	dependsOn ":extensions:portlet-configuration-icon:jar"
	dependsOn ":extensions:portlet-toolbar-contributor:jar"
	dependsOn ":overrides:login-web-resource-bundle-override:jar"
	dependsOn ":overrides:portlet-form-taglib-override:jar"
	dependsOn ":wars:jsp-war-portlet:war"

	doFirst {
		systemProperty 'actionCommandPortletJarFile', project(':apps:action-command-portlet').jar.archivePath
		systemProperty 'configurationActionJarFile', project(':apps:configuration-action').jar.archivePath
		systemProperty 'controlMenuEntryJarFile', project(':extensions:control-menu-entry').jar.archivePath
		systemProperty 'controlPanelPortletJarFile', project(':apps:control-panel-portlet').jar.archivePath
		systemProperty 'dsPortletJarFile', project(':apps:ds-portlet').jar.archivePath
		systemProperty 'filterPortletJarFile', project(':apps:filter-portlet').jar.archivePath
		systemProperty 'freemarkerPortletJarFile', project(':apps:freemarker-portlet').jar.archivePath
		systemProperty 'greedyHigherRankedServiceJarFile', project(':apps:greedy-policy-option-portlet:higher-ranked-service').jar.archivePath
		systemProperty 'greedyServiceReferenceJarFile', project(':apps:greedy-policy-option-portlet:service-reference').jar.archivePath
		systemProperty 'greedyServiceReferenceProjectdir', project(':apps:greedy-policy-option-portlet:service-reference').projectDir
		systemProperty 'servletJarFile', project(':extensions:servlet').jar.archivePath
		systemProperty 'jspPortletJarFile', project(':apps:jsp-portlet').jar.archivePath
		systemProperty 'jspPortletWarFile', project(':wars:jsp-war-portlet').war.archivePath
		systemProperty 'languageJarFile', project(':apps:shared-language-keys:language').jar.archivePath
		systemProperty 'languageWebJarFile', project(':apps:shared-language-keys:language-web').jar.archivePath
		systemProperty 'liferayHome', liferay.liferayHome
		systemProperty 'loginWebResourceBundleOverrideJarFile', project(':overrides:login-web-resource-bundle-override').jar.archivePath
		systemProperty 'portletFormTaglibOverrideJarFile', project(':overrides:portlet-form-taglib-override').jar.archivePath
		systemProperty 'osgiPortletJarFile', project(':apps:osgi-portlet').jar.archivePath
		systemProperty 'portletConfigurationIconJarFile', project(':extensions:portlet-configuration-icon').jar.archivePath
		systemProperty 'portletToolbarContributorJarFile', project(':extensions:portlet-toolbar-contributor').jar.archivePath
		systemProperty 'projectDir', rootProject.projectDir
		systemProperty 'renderCommandPortletJarFile', project(':apps:render-command-portlet').jar.archivePath
		systemProperty 'restJarFile', project(':apps:rest').jar.archivePath
	}

	systemProperty 'bladeURL', System.getProperty('bladeURL', "https://repository.liferay.com/nexus/service/local/artifact/maven/redirect?r=liferay-public-releases&g=com.liferay.blade&a=com.liferay.blade.cli&v=LATEST")

	doLast {
		delete new File("blade.jar")
		delete new File("target")
	}

	finalizedBy {
		stopTestableTomcat
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

check.dependsOn testFunctional